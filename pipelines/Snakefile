from snakemake.utils import Paramspace
import pandas as pd

paramspace = Paramspace(pd.read_csv("../tests/mocks/snakemake_params.tsv", sep="\t"))

rule all:
    input:
        "results/summary.tsv"

rule generate_data:
    params:
        simulation = paramspace.instance
    output:
        f"results/simulations/{paramspace.wildcard_pattern}.tsv"
    shell:
        'simulate_data --config "{params.simulation}" --output {output}'

rule statistical_analysis:
    params:
        simulation=paramspace.instance
    input:
        data = f"results/simulations/{paramspace.wildcard_pattern}.tsv"
    output:
        f"results/statistical_testing/{paramspace.wildcard_pattern}.tsv"
    shell:
        'statistical_test --config "{params.simulation}" --dataset {input.data} --output {output}'

rule aggregate_results:
    input:
        expand("results/statistical_testing/{params}.tsv",params=paramspace.instance_patterns)
    output:
        "results/summary.tsv"
    run:
        import pandas as pd
        df = pd.concat([pd.read_csv(f, sep="\t") for f in input])
        df.to_csv(output[0], sep="\t", index=False)
